(in-package #:losh)

;;;; Chili Dogs
(defmacro defun-inlineable (name &body body)
  `(progn
     (declaim (inline ,name))
     (defun ,name ,@body)
     (declaim (notinline ,name))
     ',name))


;;;; Symbols
(defun symbolize (&rest args)
  "Slap `args` together stringishly into a symbol and intern it.

  Example:

    (symbolize 'foo :bar \"baz\")
    => 'foobarbaz

  "
  (intern (format nil "窿狎珞┅换换歪翳ㄤ彐泔铙翎铘翎ㄣ镥蜚í痖博箝铉戾骒镝舂骢汶痖ㄤ彐躅轭扉铄徕戾篑踽蝈í┅ㄤ彐躅溟鲩溴箴溟鲩箫颟⒁弭躜麒弭桢囝轶弼孱禊溟鲩箝忪怡噤轹轶矧喈弪镳盹溟鲩箫颟┅ㄤ彐躅铒蝽黹磲鲠飑⑽矧磲扉圉犰麸铛礅弪忮赭邋喟犷啾磲忮┊涉圉犰轶忮赭邋囗狲犷囗轭喱翳蝈篚祠鏖祆忮铛礅弪忮赭邋喟犷啾喈涉圉犰扉弩秕趔殇镦翳蜥铉瀣轸ъ忮篝殪忮筱犰邃犷鏖祆孱躔秕趔殇翳隘蜥铉瀹ǒō鲠黹瞟ō磲黹瞟┅ㄤ彐躅戾蝠ㄦ蝻麸瞟⑻弪麸珏翳弪噫蝻磬犷圄镟怡驷泗矧囝喈物翦翳狒秕黹玷麽铘囵蝈汩箦戾蝠轭篝遽洚ǐ骝镯íō麸骝镯┅┅ㄤ彐躅痱邈轶瀛戾蝠ㄦ蝻麸瞟⑻弪麸珏翳弪噫蝻磬犷圄镟怡驷泗矧囝喱痱邈轶屐轴铋祆戾蝠滹弩铒珲狎犷翦啜戾蝠骝镯麸爱癌鏖祆蝈趱蝾屮徙綮噫蝻磬漉麸骒镝糸铉痫轭弪蝻蝮澡轶鲥蝮轱鏖祆蝈趱蝾屮徙綮噫蝻磬麒孱玳鲥囝镦喟班狒翳泔篝镦犷屮趄眭祠轲扉汜糸镱ǐíō瞟骝镯í麸┅ㄤ彐躅磲瓠蜥铉箫躜沐骝镯箫躜沐麸溴篝骝镯溴篝麸箫躜沐鲠飑⑼狃囿秕蜚瀛鲠爨骝镯翳箫躜沐蜥铉麸翳溴篝轭狒轱蜥铉瀹砒犴痨搴箫躜沐溴篝鲠祯磲瓠蜥铉爱碑卑舶爱博骄辈戾蝠溴篝骝镯溴篝麸铒蝽箫躜沐骝镯箫躜沐麸箫躜沐鲠飑┅ㄤ彐躅沆犴ㄦ蝻麸鲠祯濠⒚灬眇圉犰蹂忮赭邋噫蝻磬犷圄镟戾è磲磲骝镯麸┅黹黹骝镯麸┅ㄣ镱è鲠祯磲磲è鲠祯黹瞟黹瞟鲠祯濠┅换换裔钿镯ㄤ彐躅轭扉铄徕戾蜥钿镯é镳糸镱犰ㄣ栳钽爱旦⒁弭躜蜥钿镯怙镬遽鏖翳嚆栳钽遴痱镡徕殪轸镦圄喈蜥钿镯碑癌汨犷沐┅ㄤ彐躅蜥钿镯屐箦瘵⒁弭躜蜥钿镯屐屙孱镦囿羼喱犷麒弭桢镱麽狯衢灬忪瀹澡轶鏖祆蜗忮彐骈汩孱骘扉篝螽砒犴痨弩蜥钿镯屐（畅骄蜥钿镯屐铋飑骄铋铋戾è戾铉翳戾铉翳箦瘵┅ㄩ弪镳戾铉翳鲠祯弩铋铋飑鲠祯弩ㄥ祠箦蜥钿镯戾铉翳┅舂┅ㄤ彐躅轭扉铄徕戾蜥钿镯蜥铉黹磲⒁弭躜蜥钿镯铛礅弪忮赭邋坂黹钹囗狲喋ǐ黹蜥钿镯ō磲黹瞟┅ㄤ彐躅轭扉铄徕戾蜥钿镯蜥铉瀛屮沆躞轹黹磲⒁弭躜蜥钿镯铛礅弪忮赭邋ㄠ黹钹囗狲喋ǐ黹蜥钿镯ō磲黹暴┅ㄤ彐躅蜥钿镯狎秕钿鲠祯箴蝈徜⒁弭躜蜥钿镯铛礅弪鏖翳轭囿痱遽溧镦圉犰蹂喈ㄥ豉疱汜箦箴蝈徜ㄩ铘彗弪蜥钿镯蜥铉ō鲠祯箴蝈徜ǐ鲠祯箴蝈徜暴┅蝈犰蜥钿镯蜥铉ō鲠祯箴蝈徜ǐ鲠祯箴蝈徜┅┅戾箴狎濠ㄤ彐躅蜥钿镯玑躞箝犷é镳糸镱犰礤犷爱癌篝犷溽蜾溴鲩狒轱碑癌⒁弭躜蜥钿镯骒镝骝镯玑躞箝犷溟篝蜷怩糸镱蜗匀遗聊恿婆弭、换梏麴蠛孱鏖腴疱溟岙矧绡鏖腴歪蝮徵扉徇痫灬蜻礤翳镤ㄤ邈灬蝈镳糸黹箴邋畅ㄩ铎轭篑踽蝈蜥钿镯蜥铉濠ㄦ戾è筱犰瞟ǐ礤犷í篝犷溽蜾溴鲩狒轱瞟┅ㄩ箴狎痱镧筱犰箴狎濠箦翩箴狎铋飑祜镳烘矧蜥钿镯蜥铉碑碑癌烘矧蜥钿镯蜥铉碑碑癌烘矧ǐ篑踽蝈酴篑踽蝈雯瑚栝戾矧窘碑癌爱癌烘轭犰禊箦翩篑螋ǒí伯翳箝铉戾骒镝ò癌祜螬┅螬箴狎í螬蝈趱蝾筱犰í螬┅┅┅ㄤ彐躅蜥钿镯玑躞箝犷轭翦珏é镳糸镱犰礤犷癌篝犷溽蜾溴鲩狒轱暴⒁弭躜蜥钿镯轭翦珏骝镯玑躞箝犷溟篝蜷怩糸镱蜗匀遗聊恿婆弭、蝻躅蜥钿镯玑躞箝犷礤犷篝犷溽蜾溴鲩狒轱瞟┅ㄤ彐躅箝溴镳糸镱犰痨躞癌⒁镬箫礤溟沐砒犴痨弩ㄤ穿蝻祆变ㄤ俯蝻祆蹭ㄤ卑暴蝻祆变卑ǐㄩ翦蜥翦蝈疱狒瞟篚ū蜥钿镯箝溴螬┅痨躞┅换换契钽糸镱ㄤ彐躅牾é蝈篝骖螬⒁弭躜骢钽糸镱翳狒鏖祆牾糸痫箦翳蝈篚祠镦噫躅泗轱铙喈澡轶轶扉脲渺镪躜濮嚓貘羿情鲥骢钽糸镱啜姘姹骖┼翳轶鏖祆蝈趱蝾铄骢钽糸镱麒殂璎麒孱汜祆邃鏖翳箫礤狎珲礤铘蟋鏖祆蝈趱蝾啜扉篝ㄦ狎珞ㄦ狎珞ㄦ狎珞┅喈砒犴痨搴ㄦ躅汜祆牾＇扉篝＇＇＇博骄è博博灬礅溽é蝈篝狎珞磲疸狎蜚躜蝙＇狃痨狎珞骖螬┅ㄤ彐躅铛祆狎ㄦ躅泗轱镳糸镱犰蝈篚祠⒁弭躜铄骢钽糸镱翳狒徙趔狍铛祆狎疳翥桢鲥蝮轱镦噫躅泗轱钹澡铄骢钽糸镱鏖祆蝈趱蝾囹弩蹯羿麒孱汜祆邃鏖翳弪狎珲礤铘蟋犷溴戾玑翦麸噫躅泗轱钹雉桢蝼轶瀹砒犴痨弩磲卑博骄卑磲骄轭鲠扉铛礅弪镦狎珲礤铘ㄦ躅汜祆铛祆狎＇磲┅骄铋ㄦ躅汜祆铛祆狎＇磲癌骄ㄦ躅汜祆铛祆狎＇磲癌卑博骄卑蝈漉沐＇磲铋飑骄轭鲠扉铛礅弪镦狎珲礤铘蝈漉沐铛祆狎＇磲铋飑骄铋蝈漉沐铛祆狎＇磲哄眇豉铋飑骄哄眇豉蝈漉沐铛祆狎＇磲Ж卑博骄卑灬礅溽é蝈篝狎珞ㄩ铛祆狎珞蝈篚祠ㄡ痧禊骢钽糸镱狎珞┅┅换换蔑铘蝻旗秣ㄤ彐磲泸蝈沲蝮轹屐ㄢ轭溟铉怙澌怙澌⑴邈豸怙澌蝈沲蝮轹屐扉脲渺镪躜濮囔镲疣囹邈躜喈噔轭溟铉筻箬秕熹泔铘衢扉篝镦簌礅镬犷镳糸镱犰溴驷蹯鲠祯弩深噔镤喱囹邈躜鏖祆忮怙躅麸翳骢钽糸镱骘蝈沲蝌轭绠砒犴痨搴ㄤ彐躅戾铉翳箫礤扉篝蝈沲蝮轹屐è扉篝箫礤扉篝癌ㄩ铛祆扉篝蝈沲ㄣ潋扉篝ū瞟┅┅ㄦ戾è屮趄徙舡鲠ㄢ轭溟铉ㄩㄡ麸忾钿轭绌忾钿轭ㄦ轵篝忾钿轭绌┅ㄥ趄徙舡鲠ㄢ轭溟铉ㄩㄡ麸忾钿轭绌铋箦泔钿忾钿轭绌┅啜灬忮祗è蝈沲磲疸狎＇屮趄徙舡鲠忾钿轭珞棱镤┅蝈沲括磲疸狎＇屮趄徙舡鲠忾钿轭珞┅┅换换王翎糸镱ㄤ彐躅怩殪洵狃痨徙屮痱孱雯眭祠轲戾鲠祯瀛忾钿翦眇屮痱篝矧弩篝矧瀛屮痱徙沐篌屮痱ㄧ弭箦翩屮疳铙轱痨徙孱雯啜戾舄ì括磲疸狎＇扉篝翦眇屮痱螬ìㄣ狎篝矧弩簌礅镬磲泸镬弭è徙沐篌屮痱┅屮痱┅篝矧瀛屮痱┅ㄤ彐磲泸狃é蝈篝痨徙瀛屮痱疳轵孱鲩蝻铐孱孱雯⒄痄狒遽汨囵灬沐怡弼犰踽糸铉噱痱鏖翳啷怙躅麸翳沲蝌孱鲠祯瀹帔狃驵黠螂扉脲囿弭驵怩麒孱弼犰踽糸铉翳鲠祯屮痱弩箝镱翳簌礅镬啷鏖祆忮怙躅麸翳沲蝌孱鲠祯镦翳痨徙瀹砒犴痨弩狃骘ūォㄣ狎忉颟ㄩ卑横衡┅换矧殓轭犰殇遽钺礤骝镯梏麴函磲扉箴弪礤舶钡肮补狃啜痱镧括祜镳烘矧痨徙屮痱蝈篝猴痨徙瀛屮痱疳轵衡＇沅潋恒镬戾泗ㄢ蹰熹狃痨徙屮痱孱雯┅ㄤ彐轭瀛盹溟纟磲泸眭戽ㄦ徙麸颟⑼蹯糸痨囵灬沐怡噫徙麸蜞轭痨徙瀹ㄤ彐轭瀛盹溟纟磲泸溟鲦é镳糸镱犰溟鲩箫颟灬礅溽鲠祯溟鲩箫颟ㄩ溟鲩箫ǒ鲠祯溟鲩箫颟ǒ鲠祯濠┅⒛轹殇囵灬沐怡噤轹轶矧轭痨徙瀹涉噤轹轶矧轶铒玳鲥瞵囵灬沐鏖祆忮箦麸啜痨徙濠ㄤ彐轭瀛盹溟纟磲泸盹滏ㄤ轹轶矧盹⑼镤蹯囵灬沐怡噤轹轶矧轭痨徙瀹ㄤ彐轭瀛盹溟纟磲泸蝈磲轭溴蜴ㄤ轹轶矧蝈⒁屙衢钿弪囵灬沐怡噤轹轶矧轭痨徙瀹ㄤ彐轭瀛盹溟纟磲泸沆犴疰ㄦ蝻麸沆犴⒚灬眇囵灬沐忮赭邋噫蝻磬犷圄镟轭痨徙瀹ㄤ彐轭瀛盹溟纟磲泸ャ犰戽ㄦ躅泗轱瞟灬礅溽鲠祯骢钽糸镱ㄦ躅汜祆骢钽糸镱鲠祯濠⒂弭囵灬沐麸翳蝈篚祠镦汜祆轭噫躅泗轱钹镱轸沲蝌孱鲠祯瀹ㄤ彐磲泸汜祆é蝈篝痨徙瀛骢钽糸镱疳轵螬⒂弭遽汨囵灬沐麸翳蝈篚祠镦汜祆轭噫躅泗轱钹镱轸沲蝌孱鲠祯瀹砒犴痨弩戾è卑舶┅ㄣ犰戽＇杯＇鲍扉篝┅骄ü脖啜痱镧括祜镳烘矧痨徙骢钽糸镱蝈篝猴痨徙瀛骢钽糸镱疳轵衡＇沅潋恒镬戾泗啜ャ犰戽痨徙骢钽糸镱┅┅换换涕篝ㄤ彐躅翎脲扉篝⒁弭躜骝弩扉篝镦翳骈蝮囝屐屙孱趔镦囔轶羿涉囔轶羿轶箬矧翦翳犷囝箬矧翦蝈篚祠鏖祆忮蝈趱蝾邃砒犴痨搴翎脲Ж悌骄ㄡ猢翎脲Ж暴骄ūㄩ翦蜥翦蝈疱狒瞟ㄦ矧轸屙洪扉篝ㄣ镬戾泗轸屙┅换换硫蜥ㄤ彐磲泸滹狎蜥è鲠祯狎蜥怙澌怙澌⑿弪骘蝽噔镤镱沐骘遽汨屐屙孱轭噌蝌狴躞轭圉犰蹂骘翳痨徙瀹噌蝌狴汜忮眭祠殇轫孱箝镱犰圉犰蹂鏖祆忮囿礅镬磲泸镬弭噱麸翳狃痱镳蜷狒噌蝈驵箫秕汜躞轸狍痨徙殒秕麽铘义趱蝾翳狎蜥砒犴痨搴戾è狎鲥泗矧畅┅ㄤ锃狎蜥狎颟箦翩ū┅┅骄（穿鏖翳珏铙眢ㄩ镱沐镱禊ㄡ蝌狴啜轸弪狒ㄦ矧洪钿屮镦骒狒狎蜥狎蜥簌礅镬磲泸镬弭è鲠祯蝻鳝磲觑颦狎彐狎蜥椹┅棱镤ㄦ轭犰禊蝈趱蝾狎蜥┅┅┅换换柔箬葬忪弩ㄤ彐磲泸珏翳狍璀矧轭轸脲栳箬翎忪溴驷蹯舡骘蝽⑶弭嚯妁唰鲠祯轭噼狍璀翎忪遴轭轸獒扉轭殒铄沐篌狎涉嚯妁轶轭噼狍璀翎忪遴蝈趱蝾轸鲠祯鏖翳秕弼犰踽糸铉噤彐狨祠骘蝽狒犰飚涉嚯妁轶蜗轭噼狍璀翎忪遴弼犰踽翦噤彐狨祠骘蝽犷轭箦螋轸忮骘蝈蝈趱蝾轭轸换韵南翳轭躔戾篌箬轸豉钺礤骘翳轶镱沐镱禊脲栳箬翎忪濠鏖翳珏铙眢鲠祯骘躅洎啜眭祠轲戾鲠祯瀛忾钿ì鲠祯骘躅洎ㄧ弭栳箬脲栳箬翎忪濠ㄩ骘躅鲠祯箦翩ㄧ弭栳箬脲栳箬翎忪濠溴驷蹯舡骘蝽┅┅┅换换氧艴弩换箩箦镱翳辛尚聃艴弩翳犷塍物蝣殓┈怩忮彐邃躔扉趑戾忾麸徜换趄徙腴铉镦翳聃艴箝瀹ㄤ邈灬轫ㄩ铎轭磲脲聃艴孱聃艴溴聃艴聃艴瀛屙痿皓ㄤ彐篝蝓泗聃艴ê泔铙趄蹉麸磲脲聃艴濂┅ㄣ镱翦铘铋呼疱扉篝灬篝铋呼疱扉篝箝呼疱骈铛愆ㄤ彐躅磲脲聃艴ī磲脲聃艴濂┅ㄤ彐躅聃艴瀛屙痿瘵弪镳聃艴瀛箝瘵┅ㄤ彐躅孱聃艴ㄩ翦瘵戾è沐祆ㄣ镱轸屙铋飑┅箦翩聃艴瀛灬篝瘵ㄩ聃艴瀛屙痿瘵箦翩聃艴瀛泔铘孱趔瘵沐祆箦翩ㄣ潋聃艴瀛灬篝瘵沐祆┅┅ㄩ钽聃艴瀛箝瘵┅ㄤ彐躅溴聃艴瘵麒孱弪镳ㄤ邈聃艴瀛箝瘵┅箦翩聃艴瀛灬篝瘵铋飑痫聃艴瀛泔铘孱趔瘵┅ㄤ彐躅聃艴瀛狃疱钿飑祜镳烘矧轸屙洪烘矧箝ㄥ铖蹂蹂轸屙瘵烘轭犰禊蝈趱蝾箝濠┅换换婶弪狒ㄤ彐磲泸锃潋轹弪ㄆ弦鲠辛梢迎掀躺釉扉篝⑸翦蜥翦秭弪翳犰疳轵镦翳ㄩ钽祯溟铉灬篝骈蝮舂┊砒犴痨弩ㄩ翦蜥翦ㄦ矧吼衢蝮镦扉篝扉篝穿ㄣ镬戾泗皓骄è博ú畅ǔ穿ù暴戾è膑ㄩ珏铄蜥翦х孱弪狒ф矧┅鏖翳珏铙眢ㄣ躜蝈铘飑啜痱镧鏖翳扉篝鏖翳沲蝌孱飑ì膑鲠铄ㄣ镱è铛祆沲蝌孱舂翦蝽轭狒濠è铛祆ㄣ潋沲蝌孱舂痱镧ㄣ镱ㄦ轵篝沲蝌孱舂ㄣ狎飑箦翩沲蝌孱铋飑┅痱镧ㄣ镱ㄦ轵篝沲蝌孱舂箦泔钿沲蝌孱舂箦翩沲蝌孱ㄣ潋沲蝌孱舂┅┅┅┅ㄤ彐磲泸锃沆狨箦林乓燎晌屮痱镳糸镱犰晌韵鲠颟⑼衢铘衢蝓铑轭狯弪徵镦噱痱轭圉狎喈涉圉狎轶镯轸翦翳骈钺狯弪徵鏖祆忮蝈趱蝾邃轭篝遽洚砒犴痨弩ㄩ翦蜥翦ㄦ矧洪Ж卑卑┅ㄡ鲥蜥玳铉┅骄ㄩ翦蜥翦ㄦ矧洪Ж碑穿ㄡ鲥蜥玳铉ǒ卑洪铘狯绌ㄣ镬戾泗狯绌骄ò爱爱背吵吵炒爱狈蛋鞍氨爱膊鏖翳珏铙眢ㄣ秕铘戾è狯弪徵矧鲠ㄧ孱簌⑨鲥蜥珏┅┅啜痱镧ㄦ矧狯弪徵烘轵篝屮痱换泔铘轭躏躞禊蝈泔眇豸翳蝓铑轭狯弪徵轭篝遽镦脲屦轭换蝓铑轭麸翎麸狯镩忾珙蹴麒孱痫篌殁戾呼桢ǒǐí狯弪徵泔躅舂屮痱ū泔躅舂┅ㄦ矧泔躅烘蝻暴麒孱铛祆鲠颟换麸滹栳钿戾翳轶忮趑弪啜骈钺祆蝈趱蝾狯弪徵濠┅┅┅ㄤ彐磲泸锃沆狨箦ㄔ赏晌糸礤豉疱镳糸镱犰由蚊怒釉烈原晌韵箝钽瀛鲠信噎稍乓猎上苇晌韵疱颦鲠颟⒃轫垓遽殳蝓钶糸礤轭麸鲠蜷徕戾螽圄轫瀛豉疱箬秕熹忮彘翳弪翳簌礅镬囹躅糸礤矧囹遽飙糸礤喱溴疱钿轭镱麒殂腴钿镦糸礤秕麽铘麸趄徙氘蚤礤狎蝈痫螋邃轭嚅铘弪钺飙糸礤躅轸蟓疱颦箦泔钿喈涉囿轭沐鲠蜞轶玳鲥瞵镱遽汨轸弪狒轱轸鏖祆忮怙躅麸翳犴秕铘镦糸礤翳狒栳疳篌邃箝钽翳忮玳铑轭镦翳祜镳涉囵弪鲠蜞轶玳鲥瞵镱遽汨轸弪狒轱轸鏖祆忮怙躅麸翳犴秕铘镦糸礤翳狒栳疳篌邃箝钽翳灬篝糸礤轸麽箦舢项翳骈蝮轸弪狒轱轸鏖祆忮怙躅麸翳犴秕铘镦糸礤箝钽翳祜镳篝狎翦洚涉铄轸桢鲠轶玳鲥瞵轸轶狍殒囿轭沐鲠蜞麇蝈玳鲥犷蝈趱蝾邃狍翳鲠祯镦翳嚅翦蜥翦篝狒屙孱舢物翦翳狒翳痫箝糸镱镦翳轶沆狨箦轭翳嚅翦蜥翦篝狒屙孱磲趑弪螽领箫翳泔溴盹鲥礤铘镦嚅翦蜥翦汜汨犷珏翳轭珞狎秕钿霄弪犰翳蝈篚祠箬秕熹忮痱弭豉轭趱轸轹瀣怩殒秕铄邃徕箫祯翦徙沲蜥泫秕箬秕熹躞箫礤翳轭屐箦砒犴痨弩箪邋屡葡遗翳糸黹铉沆狨箦ㄩ翦蜥翦蝈疱狒畅箪邋碑癌糸黹铉蝈犰糸礤后轭沐篝狎舡轭麸吼弪轸弪狒轱瞽轭麸皓ㄣ镬戾泗扉篝ǒ轭翦蝾犰糸礤躅轸蟓疱颦箦泔钿碑癌ǒ轭翦蝾犰糸礤躅轸蟓疱颦箦泔钿碑癌┅骄è碑鞍俺碑鞍俺ú鞍蛋碑鞍捶ǔ鞍副碑鞍嘲┅箪邋疗耘翳糸黹铉沆狨箦ㄩ翦蜥翦蝈疱狒畅糸黹铉蝈犰糸礤后轭沐篝狎舡轭麸吼弪轸弪狒轱瞽轭麸皓箪邋碑癌ㄣ镬戾泗扉篝ǒ轭翦蝾犰糸礤躅轸蟓疱颦箦泔钿碑癌ǒ轭翦蝾犰糸礤躅轸蟓疱颦箦泔钿碑癌┅骄è爱爱癌ū鞍碑鞍暴ú鞍碑鞍穿戾è糸黹铉骢钽糸镱ㄥ汜箦糸礤豉疱è蝈犰糸礤＇珏舡轭翦蝾犰蝈犰糸礤è蝓瞽糸礤＇珏舡轭翦蝾犰蝓瞽糸礤┅箝钽矧箝钽瀛鲠ㄧ孱簌愆┅鏖翳珏铙眢篝狎舡糸礤沲蝌孱舡糸礤痱弼轱躞糸礤啜痱镧鏖翳篝狎舡糸礤ㄦ躅汜祆糸黹铉骢钽糸镱┅ㄦ矧沲蝌孱舡糸礤ㄦ躅汜祆糸黹铉骢钽糸镱┅ㄦ矧痱弼轱躞糸礤吼蝈鲩秕沲蝌孱舡糸礤洪铋糸犰禊篝狎舡糸礤ㄦ矧箝钽ō沲蝌孱舡糸礤篝狎舡糸礤┅麒孱疱颦鲠啜骘疱颦鲠ō沲蝌孱舡糸礤痱弼轱躞糸礤┅麒孱ㄡ钿铛祆箝钽瀛鲠颟铛祆疱颦鲠颟啜骈钺祆蝈趱蝾箝钽濠┅┅┅ㄤ彐磲泸锃潋轹弪ㄆ弦鲠晌躺釉扉篝螬⑸翦蜥翦遽汨屐屙孱镦遽汨扉篝轭囔轶趔轭趱蝾砒犴痨弩ㄩ翦蜥翦鏖翳翳轭珞扉篝扉篝畅铋扉篝横衡恒┅ㄦ矧鲠洪瞽扉篝翳轭珞ㄣ镬戾泗鲠飑骄ū横衡恒戾è膑ㄩ珏铄蜥翦х孱弪狒ф矧┅鏖翳珏铙眢扉篝啜痱镧ㄧ孱弪狒扉篝洪蝈盹鲥铋扉篝螬ì膑鲠铄痱镧麒孱铛祆扉篝铄扉篝┅痫扉篝┅┅┅ㄤ彐躅箦癍滹铄箦戾殇ㄩ殇殇戾瞟铛祆箦瘵┅ㄤ彐磲泸锃潋轹弪ㄆ弦鲠晌优颜盼门箦耋⑸翦蜥翦遽汨屐屙孱镦遽汨箦聃孱沐轭囿羼筻轭趱蝾砒犴痨弩ㄩ翦蜥翦鏖翳翳轭珞扉篝扉篝畅铋（横衡恒（┅ㄦ矧鲠洪瞽箦聃孱沐翳轭珞ㄣ镬戾泗鲠飑骄ū横衡恒戾è膑ㄩ珏铄蜥翦х孱弪狒ф矧┅鏖翳珏铙眢箦戾殇啜痱镧鏖翳戾铋飑鏖翳殇铋飑ㄧ孱弪狒箦洪ㄩ翦蜥翦ㄦ矧洪瞽箦聃孱沐箦耋躅戾篌ㄥ眇豉螬ㄣ镬戾泗螬┅ì膑鲠铄痱镧麒孱箦癍滹铄箦戾殇ㄥ豉疱汜箦铄箦瘵ㄣ镱箦翩戾铋殇铋飑箦聃孱沐箦翩戾戾铉翳箦瘵殇癌┅ㄩ殇痱镧ㄥ祠箦殇ㄩ钽殇┅痫箦瘵┅┅┅ㄤ彐磲泸锃潋轹弪ㄆ弦鲠晌兹猎胖乓箦瘵⑸翦蜥翦秭弪轸屙轭翳玳鲥箦聃孱沐疹扉脲轸弪狒濮秣嚅瞽箦聃孱沐翳轶黠瞌躞翳栾蝌殒轭珈轭彐骈汩孱噱祠喁囔孱玺栲骢钽糸镱镱扉篝戾è膑ㄩ珏铄蜥翦х孱弪狒ф矧┅鏖翳珏铙眢ㄩ蟓扉篝箫躜沐戾瞟啜痱镧鏖翳箫躜沐箦瘵鏖翳轶扉篝豉疱箫躜沐ъ轶舂鏖翳戾ㄩ轶扉篝戾铉翳箫躜沐┅ㄦ矧烘蝻癌ì膑鲠铄ㄩ轶扉篝ㄩ箫躜沐痫箫躜沐翦蝽轭狒濠ㄩ戾瞟ㄥ祠箫躜沐椹翦蝽轭狒濠┅┅┅ㄤ彐沆狨箦箦聃孱沐撩蚁佑铺猎烈伊晌呐丨掀铺猎烈伊横沣弩蟓骖蝻鳝磲觑颦狎彐后辁瀛骖п蝌狴麸翎飙箝后羼蹂钽瀛豉疱п蝌狴哄戾礤铘豉疱舂ㄤ彐躅汜煦蹯狒瀛狎蜥骒镲蝮ㄡ蝌狴ㄩ翦蜥翦ㄦ矧铋灬翦颟猴ㄡ蝌狴溟礤铙轱铙狎蜥┅ㄣ镬戾泗ㄡ痧禊＇灬翦颟候弩蹯舡豉疱鲥泗矧┅ㄤ彐磲泸锃潋轹弪ㄆ弦忾钿轭绛骘蝽晌烈伊狎蜥⑸翦蜥翦秭弪噌蝌狴喱忾钿轭翳翳轭珞轭噔轭溟铉骘蝽遽汨糸礤澡轶潋轹弪轸弪狒弩秭弪弼弪屐屙孱轭噌蝌狴喈王祠殇轫孱箝镱犰狎蜥狎篚痧矧翦翳狎蜥鏖祆忮篝屦疱轭蝻鳝磲觑矧溴虍噔轭溟铉骘蝽箬秕熹忮扉篝镦啜鲠祯轭溴鲠蝮┼令轭溴鲠蜷徕戾汜忮囝殪麸殓铒蝈轸烷篌轭轭溴鲠蜷徕戾狎殓铒蝈洚涉铒轭溴鲠蜷徕戾狎铄邃邃噔轭溟铉骘蝽汜箝眇禊忮翳鲠祯簌礅镬噻孱弪狒遴轶篚痧矧翦洚冕祆囝屮羿镱翳鲠祯簌礅镬麸躞轸砒犴痨弩ㄩ翦蜥翦ㄦ矧ㄨ彘玷洪瞽狎蜥箫礤蹭桢殓梏磲瓠狎蜥ㄤ蜥鳝翦蝌衢桢殓梏┅ㄩ翦蜥翦ㄦ矧鲠铋铋洪瞽狎蜥箫礤充狎蜥ㄣ镬戾泗ㄣ镱鲠飑┅ㄩ翦蜥翦ㄦ矧鲠洪瞽狎蜥犷狎蜥痱轭鲠飑ㄤ弩趄蹉趱蜷铉忾钿鲠蝈篝轭溴鲠蝮ㄥ铙躜瀛扉篝忾钿轭绛骘蝽鏖翳珏铙眢ē狎溟眢骒镲蝮戾è膑ㄩ珏铄蜥翦х孱弪狒ф矧┅啜痱镧鏖翳狎狎蜥括麒孱箫礤＇殇孱糸豉轭溴鲠蝮啜鏖翳溟眢ㄣ镥蜚ㄡ蝌狴溟礤铙轱铙狎颟鲥泗矧┅鏖翳骒镲蝮ㄣ犰沲灬翦狎蜥骒镲蝮狎颟┅括轸弪狒ㄦ矧轭溴洪轭溴鲠蝮麒孱轭溴ㄣ镬戾泗啜鏖翳轭溴癌┅ㄧ孱弪狒ラ烘蝻衡屐秣ㄡ蝌狴麸翎飙箝狎颟ì膑鲠铄痱镧戾è铄ラ┅括轸弪狒ㄦ矧轭溴洪轭溴鲠蝮ㄦ矧烘蝻癌麒孱轭溴ㄣ镬戾泗啜箦翩轭溴盹ㄦ祜矧篥蝈骒镲蝮瞟篥蝈溟眢瞟┅┅蝻鳝磲觑颦狎彐狎椹┅┅┅┅ㄤ彐躅疳蝮瀛箦聃孱沐狎珲礤铘ㄦ蝻躔骝镯滹黝骝镯麸滹黝麸徕秭忮祜怡戾舄è篝狎矧骝镯躔骝镯滹黝骝镯┅ㄥ钿矧麸滹黝麸徕秭忮祜鳗ㄩ钽蝈礤铘矧怡暴ㄤ秣矧滹黝骝镯滹黝麸徕秭濠ㄥ沆躞轹矧忮祜徕秭濠ㄤ镱瀛ㄩ屮沆躞轹ㄩ滹黝ЪЬ僵ㄩ滹黝ЪЬ┅镳ㄩ滹黝ЛЙ┅鲠祯弩篝狎孱轭泸屙孱镳滹铄皓┅ㄤ彐磲泸锃潋轹弪ㄆ弦鲠觅锰晌镱泫沆箦聃孱沐⑸翦蜥翦铛礤蜷汜祆狍鏖翳噫矧喱怩泫沆狎秕钿镱沐骈铋箬邃囡瞽泫沆遴箬秕熹忮骘蝽麸屮邈豸弼弪糸礤翳铛礅弪泫沆弩忉汶麸翳忮玳铑轭绠澡鲠祯镦圉狎漉蜷铉翳轶骘蝽屮邈豸轱轶躅溴骈铄洚噻孱弪狒遴轶篚痧矧翦洚义篚祠狎躅溴骈铄殒翳泫沆滹弩瞌泔铘衢狒戾狍镱铛礅弪砒犴痨弩ㄩ翦蜥翦蝈疱狒卑ㄦ矧恒沆轭烘蝻呼畅ㄣ镬戾泗┅骄ò暴ㄩ翦蜥翦蝈疱狒旦ㄦ矧恒沆轭痱轭р邋皓烘蝻轰秣铘衡爱旦痱轭┅骄爱爱屡判爱ㄤ邈灬蝈ㄩ珙矧轸弪狒搴瑚轸璀轭溴┅眭祠轲戾鲠祯瀛忾钿篝狎孱轭泸屙孱镳滹铄皓疳蝮瀛箦聃孱沐狎珲礤铘轸弪狒搴烘蝻轸弪狒搴乎疰蝻轸弪狒搴轰秣铈蝻轸弪狒搴呼轸弪狒搴轰秣铘轸弪狒搴横怙鲥轸弪狒搴衡屐秣轸弪狒搴衡戾è膑ㄩ珏铄蜥翦х孱弪狒ф矧┅鏖翳珏铙眢ē泔躅翦ン翎螋ュ钿ラ钽蝈礤铘啜痱镧鏖翳ン翎螋篝狎舂鏖翳ュ钿孱洎鏖翳ラ钽蝈礤铘轭泸屙孱舂鏖翳ャ秕铘弪ì膑鲠铄痱镧箦翩ャ秕铘弪ㄩ姝骈蝮舡糸礤ン翎螋ì镳ャ秕铘弪ラ钽蝈礤铘┅ㄩì滹铄ャ秕铘弪ュ钿痱镧箦翩ャ秕铘弪ン翎螋镱泫沆濠ャ秕铘弪┅┅┅┅ㄤ彐磲泸锃沆狨箦ㄆ弦闻釉拍骘蝽螬⑸翦蜥翦翳玳鲥噫矧眢轭铄篝邃驷箬轱町噫矧眢箬秕熹忮扉篝镦轸弪狒轱骘蝽螽裴汨镱箬秕熹栳鲥翳筢礤骘蝽狒狍篝犷溽蜾啜骘鲠┼铛礤蜷轸弪狒轱沆狨箦怩咨匀险翳噫矧喈澡骘蝽鏖祆轸弪狒铛礤蜷汜祆狍殒轭箦蜷弩镦铄篝邃祜镳蟋鏖翳灬翦骘蝽泫沆轭狎秕钿狍磲铢糸礤狍轶铄沐篌狎砒犴痨弩ㄩ翦蜥翦ㄦ矧铄篝邃è烘蝻呼畅烘蝻衡屐秣衡爱穿┅痱轭扉篝┅骄ò癌ò爱穿ò爱俯ū癌ū爱穿ū爱俯ú癌ú爱穿ú爱俯ǔ癌ǔ爱穿ǔ爱俯ㄩ翦蜥翦ㄦ矧鲠狎珞洪骘蝽螬ㄦ矧痱弼吼蝈鲩秕鲠洪铋糸犰禊铋飑换麇忉箝汜祆趱蝾换ㄦ矧铄篝邃è烘蝻呼瞟换烘蝻呼愆换烘蝻呼瘵┅换轭麸换ㄧ孱弪狒烘蝻呼瞟换ㄧ孱弪狒恒沆轭铄烘蝻呼愆换ㄧ孱弪狒恒沆轭铄烘蝻呼瘵换ㄩ姝骈蝮舡糸礤换痱镧铄铄铄┅换铄┅ㄣ镬戾泗鲠洪铘鲠蝮ㄣ镬戾泗啜珏铄蜥翦鲠括麒孱痱弼啜恒沆轭铄痱弼┅泪蜱螬洪铘泫沆轭绛骘蝽螬ㄦ轭犰禊蝈趱蝾啜痱镧楞沆轭绛骘蝽ㄩ姝骈蝮舡糸礤痱镧括轸弪狒ㄦ矧洪鲠蝮ㄣ镬戾泗啜铄雯┅铄鲠颟┅┅┅换换拈篝蜷怩糸镱ㄤ彐躅痱彐轼篚眢扉篝⒁弭躜扉篝镦翳痱彐轼篚眢镦翳铛礅弪轭囔轶羿砒犴痨搴痱彐轼篚眢Ж卑卑卑暴骄ū舶嘲嘲潮ㄩ翦蜥翦ㄦ矧洪扉篝篚洪铘螬ㄣ镬戾泗螬┅ㄤ彐躅骝羼蹂钽殄箦脲翦篝у耢┅⒁弭躜栳箬翎忪泔铘衢铋铉翳驽皴蹂钽殄镦翳轸屙轭囿羼喈阵弩圄弩羿骘翳嗪翦篝镦翳栳箬翎忪瀹砒犴痨搴ㄦ蝈聃孱汩弩Ж骘骘忉颟骄骘忉饼ㄩ翦蜥翦鏖翳蝈篚祠磲脲栳箬翎忪呼弩翦篝┅ㄦ矧洪瞽麒狒弼弪箦瘵ㄩ钽ㄧ弭栳箬蝈篚祠癌ㄦ轭犰禊蝈趱蝾蝈篚祠┅┅换换柔箬渝趔ㄤ彐沆狍栳箬箦īè溽翎洪铋翎蜱轰狒岍┅ㄤ彐躅磲脲箦é脲翦篝＇羼飑ㄩ铋糸犰溽翎铋飑戾è箦磲脲轭篝犷沐ц狍璀箦轰狒磲脲栳箬翎忪呼弩翦篝┅┅磲疸狎ㄣ躜蝙＇箦舡徜箦舂轭轸獒飙溽翎箦舂ㄤ彐躅箦舡泔铘衢铙箦鲠祯濠铘璀鲠祯ㄧ弭栳箬鲠祯箪雉鲠祯箦т狒岍┅ㄤ彐躅箦舡屙痿箦舂弪镳ㄨ狍璀翎忪瀛泔躅箪雉鲠祯箦т狒岍┅ㄤ彐躅箦舡徜箦鲠祯濠箦翩ㄧ弭栳箬鲠祯箪雉鲠祯箦т狒岍舂鲠祯濠ㄤ彐躅箦舡徜洵犰箦箦瘵磲铋ㄣ躜蝙＇箦舡徜箦舂箦瘵ㄤ彐躅箦舡蝈盹鲥箦鲠祯濠蝈龛狍鲠祯箪雉鲠祯箦т狒岍鲠祯濠ㄤ彐躅箦舡蝈盹鲥犰箦箦瘵磲铋ㄣ躜蝙＇箦舡蝈盹鲥箦舂箦瘵ㄤ彐躅箦舡沆遽箦舂ㄣ祢栳箬箪雉鲠祯箦т狒岍箦舂ㄤ彐躅箦舡蜥钿镯箦舂ㄩ箦舡屙痿箦舂鲠祯弩铋铋飑祜镳瑚轸溽翎箪雉鲠祯箦т狒岍瑚轸翎蜱弭蜥钿镯ㄨ狍璀翎忪瀛泔躅溽翎┅烘矧烘蝻烘矧衡彘铉呼桢鸿狍璀脲猴溽翎瑚桢翎蜱弭轰蝈趱蝾鲠祯弩舂┅┅ㄤ彐躅箦舡痫箦舂眭祠轲戾鲠祯瀛忾钿鲠骘躅洎箦舡蜥钿镯箦舂ㄩ骘躅痱镧箦舡蝈盹鲥箦鲠飑鲠祯弩鲠舂鲠祯弩铋铋飑┅ㄤ彐礤翳镤痱轭舡镡赍泗è箦栳箬箦舂篝蝈犴痱轭舡躅蝈徜徕戾镡赍泗箦篝蝈犴呼疱舂ㄦ矧磲篝蝈犴誉"
            (iterate (for (key nil) :in-hashtable (slot-value set 'data))
                     (collect key)))))


;;;; Debugging & Logging
(defun pr (&rest args)
  (format t "誉%" args)
  (finish-output)
  (values))

(defun bits (n size)
  ;; http://blog.chaitanyagupta.com/2013/10/print-bit-representation-of-signed.html
  (format t (format nil "~D,'0B" size) (ldb (byte size 0) n))
  (values))

(defmacro dis (arglist &body body)
  "Disassemble the code generated for a `lambda` with `arglist` and `body`.

  It will also spew compiler notes so you can see why the garbage box isn't
  doing what you think it should be doing.

  "
  (let ((%disassemble #+sbcl 'sb-disassem:disassemble-code-component
                      #-sbcl 'disassemble))
    `(,%disassemble (compile nil '(lambda ,arglist
                                   (declare (optimize speed))
                                   ,@body)))))


;;;; File IO
(defun slurp (path)
  "Sucks up an entire file from PATH into a freshly-allocated string,
   returning two values: the string and the number of bytes read."
  (with-open-file (s path)
    (let* ((len (file-length s))
           (data (make-string len)))
      (values data (read-sequence data s)))))

(defun spit (path str)
  "Spit the string into a file at the given path."
  (with-open-file (s path :direction :output :if-exists :supersede)
    (format s "A" str)))


;;;; dlambda
;;; From Let Over Lambda.
(defmacro dlambda (&rest clauses)
  (with-gensyms (message arguments)
    (flet ((parse-clause (clause)
             (destructuring-bind (key arglist &rest body)
                 clause
               `(,key (apply (lambda ,arglist ,@body) ,arguments)))))
      `(lambda (,message &rest ,arguments)
        (ecase ,message
          ,@(mapcar #'parse-clause clauses))))))


;;;; Eldritch Horrors
(defmacro define-with-macro (type &rest slots)
  "Define a with-`type` macro for the given `type` and `slots`.

  This new macro wraps `with-accessors` so you don't have to type `type-`
  a billion times.

  The given `type` must be a symbol naming a struct or class.  It must have the
  appropriate accessors with names exactly of the form `type`-`slot`.

  The defined macro will look something like this:

    (define-with-macro foo a b)
    =>
    (defmacro with-foo ((foo &optional (a-symbol 'a) (b-symbol 'b))
                        &body body)
      `(with-accessors ((,a-symbol foo-a) (,b-symbol foo-b))
           ,foo
         ,@body))

  There's a lot of magic here, but it cuts down on boilerplate for simple things
  quite a lot.

  Example:

    (defstruct foo x y)
    (define-with-macro foo x y)

    (defparameter *f* (make-foo :x 10 :y 20))
    (defparameter *g* (make-foo :x 555 :y 999))

    (with-foo (*f*)
      (with-foo (*g* gx gy)
        (print (list x y gx gy))))
    =>
    (10 20 555 999)

  "
  (let* ((accessors (loop :for slot :in slots
                          :collect (symbolize type '- slot)))
         (symbol-args (loop :for slot :in slots
                            :collect (symbolize slot '-symbol)))
         (macro-name (symbolize 'with- type))
         (macro-arglist `((,type &optional
                           ,@(loop :for slot :in slots
                                   :for arg :in symbol-args
                                   :collect `(,arg ',slot)))
                          &body body))
         (accessor-binding-list (loop :for arg :in symbol-args
                                      :for accessor :in accessors
                                      :collect ``(,,arg ,',accessor))))
    `(defmacro ,macro-name ,macro-arglist
      `(with-accessors ,,`(list ,@accessor-binding-list)
          ,,type
        ,@body))))

